buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven {
            name = 'sponge'
            url = 'http://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.4+'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.

version = "0.2.0-preview9"
group = "com.aranairra.arcanearchives"
archivesBaseName = "arcanearchives"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

minecraft {
    version = "${minecraft_version}-${forge_version}"
    mappings = "${forge_mappings}"
    if (file('../run').exists()) {
        runDir = "../run"
    } else {
        runDir = "run"
    }    

    useDepAts = true
}

repositories {
    maven {
        name = "JEI"
        url =  "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        name = "HWYLA"
        url = "http://tehnut.info/maven"
    }
    maven { // The One Probe
        name 'tterrag maven'
        url "https://maven.tterrag.com/"
    }
    maven { // Quark
        name = "BlameJared"
        url = "https://maven.blamejared.com"
    }
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }
    maven { url = 'https://repo.spongepowered.org/maven' }
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    deobfCompile "mezz.jei:jei_${project.jei_version}"
    //runtime "mezz.jei:jei_${project.jei_version}"
    deobfCompile "mcp.mobius.waila:Hwyla:${project.hwyla_version}"
    deobfCompile "mcjty.theoneprobe:TheOneProbe-${top_version}"
    deobfCompile "vazkii.quark:Quark:${quark_version}"
    shade('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        transitive = false
    }
}

runClient {
    args '--username', 'archives'
}

jar {
    manifest {
        attributes 'FMLAT': 'aa_at.cfg'
        attributes "TweakClass": "org.spongepowered.asm.launch.MixinTweaker"
        attributes "TweakOrder": 0
    }

    configurations.shade.each { dep ->
        from(project.zipTree(dep)) {
            rename 'LICENSE.txt', 'org/spongepowered/LICENSE.txt'

            exclude 'META-INF/*.SF'
            exclude 'META-INF/*.RSA'
            exclude 'META-INF/*.MF'

            exclude '**/*.html'
        }
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

mixin {
    add sourceSets.main, "mixins.arcanearchives.refmap.json"
}